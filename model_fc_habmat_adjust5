
model {
  # Priors
  psi ~ dunif(0, 1)                               # M*psi = E[N(1)], for data augmentation
  phi ~ dunif(0, 1)                               # Survival
  gamma ~ dunif(0, 3)                             # Per-capita recruitment
  
  # Prior for baseline cap prob # binomial 0 to 1
  p0 ~ dbeta(1.3, 3.7)
  
  # Prior for scale parameter # poisson dist
  sigma ~ dgamma(3, 1)
  
  # Derived parameters
  for (t in 1:T){
    N[t] <- sum(z[,t])                            # Population size at time t
    EB[t] <- N[t] * gamma                         # Expected number of recruits
    A[t] <- max(M - sum(a[,t]), 0.001)            # Bears available to be recruited
    b[t] <- min(EB[t] / A[t], 0.999)              # Probability of being recruited
  }
  
  # Population model (individual-based model)
  # Initial state
  for (i in 1:M){
    z[i,1] ~ dbern(psi)                           # Is a member of M alive?
    a[i,1] <- z[i,1]                              # Recruited yet?
    
    # State model: point process model
    AC[i, 1] ~ dunif(1, upperLimit[1])    # x-coord of activity centers
    AC[i, 2] ~ dunif(1, upperLimit[2])    # y coord of activity centers
    hab[i] <- habMat[trunc(AC[i, 1]), trunc(AC[i, 2])] # habitat look-up
    ones[i] ~ dbern(hab[i])               # ones[i] = 1, the ones trick
    
    #s[i,1] ~ dunif(xlims[1], xlims[2])            # Activity center, homogeneous
    #s[i,2] ~ dunif(ylims[1], ylims[2])
    
    # Dynamics over time
    for (t in 2:T){
      z[i,t] ~ dbern(z[i,t-1] * phi + (1 - a[i,t-1]) * b[t-1])
      a[i,t] <- max(z[i,1:t])
    } #t
  } #i
  
  # Observation models
  # Spatial capture-recapture data
  for (i in 1:M){
    for (j in 1:J){
      #d2[i,j] <- (s[i,1] - x[j,1])^2 + (s[i,2] - x[j,2])^2  # Distance squared
      d2[i,j] <- (AC[i,1] - trapMat[j,1])^2 + (AC[i,2] - trapMat[j,2])^2           # distance^2
      p[i,j] <- p0 * exp(-d2[i,j] / (2 * sigma^2))
    } #j
    for (j in 1:J){                               # Trap
      for (k in 1:K){                             # Secondary occasions
        # The years with SCR data
        y[i,j,k,1] ~ dbern(p[i,j] * z[i,1])
        y[i,j,k,2] ~ dbern(p[i,j] * z[i,2])
        y[i,j,k,3] ~ dbern(p[i,j] * z[i,3])
      } #k
    } #j
    zi[i] <- (sum(z[i,]) > 0)                     # Was this bear ever alive?
  } #i
  
  # Occupancy data
  #for (j in 1:J){
  #  for (k in 1:K){
  #    o[j,k,1] ~ dbern(1-prod(1-p[,j] * z[,2]))
  #    o[j,k,2] ~ dbern(1-prod(1-p[,j] * z[,4]))
  #    o[j,k,3] ~ dbern(1-prod(1-p[,j] * z[,6]))
  #  } #k
  #} #j
  N.ever <- sum(zi[])                             # Bears ever alive â€“ superpopulation size
}
